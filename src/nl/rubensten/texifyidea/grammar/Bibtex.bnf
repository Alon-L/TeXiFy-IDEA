{
    parserClass="nl.rubensten.texifyidea.parser.BibtexParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Bibtex"
    psiImplClassSuffix="Impl"
    psiPackage="nl.rubensten.texifyidea.psi"
    psiImplPackage="nl.rubensten.texifyidea.psi.impl"

    elementTypeHolderClass="nl.rubensten.texifyidea.psi.BibtexTypes"
    elementTypeClass="nl.rubensten.texifyidea.psi.BibtexElementType"
    tokenTypeClass="nl.rubensten.texifyidea.psi.BibtexTokenType"
}

bibtexFile ::= (entry | comment)*

entry ::= TYPE_TOKEN OPEN_BRACE id? (tag SEPARATOR)* tag ENDTRY

id ::= comment* IDENTIFIER comment* SEPARATOR

tag ::= comment* key comment* ASSIGNMENT comment* content comment*

key ::= IDENTIFIER

content ::= (string (CONCATENATE string)+) | string | NUMBER | IDENTIFIER

string ::= defined_string | quoted_string | braced_string

defined_string ::= IDENTIFIER

quoted_string ::= QUOTES NORMAL_TEXT QUOTES

braced_string ::= OPEN_BRACE NORMAL_TEXT+ CLOSE_BRACE

comment ::= COMMENT_TOKEN